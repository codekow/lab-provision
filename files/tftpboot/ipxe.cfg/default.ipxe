#!ipxe

set esc:hex 1b
set bold ${esc:string}[1m
set boldoff ${esc:string}[22m
set fg_gre ${esc:string}[32m
set fg_cya ${esc:string}[36m
set fg_whi ${esc:string}[37m

# console --picture http://boot.ipxe.org/ipxe.png --left 32 --right 32 --top 32 --bottom 48 ||

# Figure out if client is 64-bit capable
cpuid --ext 29 && set arch x64 || set arch x86

# Some menu defaults
set menu-timeout 10000
set submenu-timeout ${menu-timeout}
isset ${menu-default} || set menu-default exit

:dhcp
isset ${ip} || dhcp || echo DHCP failed

###################### MAIN MENU ####################################

:start
menu iPXE Menu [IP: ${netX/ip} on ${netX/chip} ${initiator-iqn}] ${platform}
item --gap -- -----------
item exit Local HD
item netboot netboot.xyz
item --gap -- -----------
item ks-fedora Kickstart Fedora
item memtest Memtest
item debug iPXE Shell
item --gap -- -----------
item reboot Reboot System
item netconfig Manual Network Config

choose --timeout ${menu-timeout} --default ${menu-default} selected || goto exit
set menu-timeout 0
goto ${selected}

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
sleep 6
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

:back
set submenu-timeout 0
clear submenu-default
goto start

:netconfig
echo Network Configuration:
echo Available interfaces...
ifstat
imgfree
echo -n Set network interface number [0 for net0, defaults to 0]: ${} && read net
isset ${net} || set net 0
echo -n IP: && read net${net}/ip
echo -n Subnet mask: && read net${net}/netmask
echo -n Gateway: && read net${net}/gateway
echo -n DNS: && read dns
ifopen net${net}
goto start

:netboot
chain --autofree http://boot.netboot.xyz || goto failed

:memtest
chain memtest.ipxe

:ks-fedora
chain ks-fedora.ipxe || goto failed
